rule dynamicbeast:
    """
    Use `dynamicbeast <https://github.com/Wytamma/dynamic-beast>`_ to generate
    a dynamic BEAST2 XML template from a standard static one, for use with
    `feast <https://github.com/tgvaughan/feast>`_.

    :input template: the BEAST XML template file
    :input phytest_report:  the phytest report file generated by the :smk:ref:`phytest` rule.
                            By using this as an input, we ensure that this rule (and all downstream rules)
                            only run when our quality control checks pass.

    :output:  the dynamic XML template file to be used by the :smk:ref:`onlinebeast` and/or :smk:ref:`beast` rules
    """
    input:
        template=RESOURCES_DIR / "template.xml",
        phytest_report="results/{id}-phytest.html",
    output:
        "results/beast/{id}-dynamic_beast.xml",
    log:
        "logs/{id}-dynamic_beast.log",
    conda:
        "../envs/dynamicbeast.yml"
    shell:
        """
        dynamic-beast {input.template} > {output} 2> {log}
        """

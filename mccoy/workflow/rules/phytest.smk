rule phytest:
    """
    Run quality control checks using `phytest <https://github.com/phytest-devs/phytest>`_.

    :input alignment:     the aligned fasta file from the :smk:ref:`align` rule
    :input tree:          the maximum likelihood tree generated by the :smk:ref:`tree` rule
    :input phytest_file:  the phytest test file

    :output:  The phytest html report, placed into the Snakemake report
    """
    input:
        alignment="results/aligned/{id}.fasta",
        tree="results/tree/{id}.fasta.treefile",
        phytest_file=PROJECT_DIR / "tests.py",
    output:
        report("results/{id}-phytest.html", category="Quality Control"),
    log:
        "logs/phytest-{id}.log",
    conda:
        "../envs/phytest.yml"
    shell:
        """
        phytest {input.phytest_file} -s {input.alignment} -t {input.tree} --report {output} -v > {log}
        """
